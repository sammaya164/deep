---
categories: "mnist"
title: "手書き数字画像データ"
excerpt: "MNIST/train-images.idx3-ubyte/train-labels.idx1-ubyte/訓練用画像を確認する"

---

## MNIST

ニューラルネットワークの訓練と評価に使用するため、MNISTデータベースを[ここ](http://yann.lecun.com/exdb/mnist/)からダウンロードする。

- train-images-idx3-ubyte.gz
- train-labels-idx1-ubyte.gz
- t10k-images-idx3-ubyte.gz
- t10k-labels-idx1-ubyte.gz

圧縮されているので、7-Zipなどを使って展開する。

|ファイル名|説明|byte|
|---|---|---|
|train-images.idx3-ubyte|60,000枚分の訓練用画像|47,040,016|
|train-labels.idx1-ubyte|訓練用画像の正解ラベル|60,008|
|t10k-images.idx3-ubyte|10,000枚分の評価用画像|7,840,016|
|t10k-labels.idx1-ubyte|評価用画像の正解ラベル|10,008|

これらはバイナリファイルである。1枚ずつの画像ファイルではない。

## train-images.idx3-ubyte

1画像は横28×縦28=784ピクセルから成る。

1ピクセルは0～255のグレースケールであり、1byteで表現する。

1画像は784byteである。

冒頭の16byte+784byte/枚×60,000枚で47,040,016byteとなっている。

t10k-images.idx3-ubyteも同様。

## train-labels.idx1-ubyte

画像が表す数値が0～9のいずれであるかを表している。

1画像あたり1byteである。

冒頭の8byte+1byte/枚×60,000枚で60,008byteとなっている。

t10k-labels.idx1-ubyteも同様。

## 訓練用画像を確認する

Streamオブジェクトを使用して、バイナリファイルを確認するプログラムを作成した。

```vb
Dim input1
Dim input2

'MNISTデータベースファイルをC:\testに保存している場合
input1 = "C:\test\train-images.idx3-ubyte"
input2 = "C:\test\train-labels.idx1-ubyte"

Dim images
Dim labels

'バイナリ形式でファイルを開く
Set images = CreateObject("ADODB.Stream")
Set labels = CreateObject("ADODB.Stream")
images.Type = 1 'BINARY
labels.Type = 1 'BINARY
images.Open
labels.Open
images.LoadFromFile(input1)
labels.LoadFromFile(input2)

Dim myVal
Dim label
Dim image(783)
Dim i
Dim buf

Randomize '乱数ジェネレータを初期化

'キャンセルボタンが押されるまで繰り返す
Do
    myVal = Int((Rnd * 60000) + 1) '1～60000の乱数
    images.Position = 16 + 784 * (myVal - 1)
    labels.Position = 8 + (myval - 1)

    '1画像の各ピクセルデータを1次元の配列に格納する
    For i = 0 To 783
        image(i) = AscB(images.Read(1))
    Next

    '正解の数値
    label = AscB(labels.Read(1))

    '画像をダイアログボックス上に疑似的に表示する
    buf = ""
    For i = 0 To 783
        If image(i) > 128 Then
            buf = buf & "■"
        Else
            buf = buf & "□"
        End If

        If (i + 1) Mod 28 = 0 Then
            buf = buf & vbCr
        End If
    Next

    If Msgbox(buf & vbCr & "正解: " & label, vbOKCancel, "No." & myVal) = vbCancel Then
        Exit Do
    End If
    
Loop

images.Close
labels.Close
```

![実行画面](/assets/images/210304-mnist.png)
