Option Explicit

Dim w1, b1, w2, b2 '重みとバイアスのパラメータ
Dim projectName 'プロジェクト名
Dim numLearn '学習回数

Select Case WScript.Arguments.Count
Case 0 'Wクリックで起動した場合
    Call InitParameters()
    Call OutputParameters()

Case 1 'ドラッグ＆ドロップで起動した場合
    Call LoadParameters(WScript.Arguments.Item(0))

    Select Case MsgBox("学習する　→はい" & vbCr & "推論する　→いいえ", vbYesNoCancel, "学習しますか")
    Case vbNo
        Call Suiron()
        Call OutputAccuracy()
    Case vbYes
        Call Gakushu()
        Call OutputParameters()
    Case Else
        WScript.Quit()
    End Select

Case Else '複数ファイルの場合は終了
    WScript.Quit()

End Select



'パラメータの新規作成
Function InitParameters()
    projectName = Inputbox("プロジェクト名を入力して下さい", "パラメータの新規作成", "Test")
    If projectName = "" Then WScript.Quit 'キャンセルボタンが押された場合は終了
    projectName = Replace(projectName, "_", "-") '_は-へ変換する
    numLearn = 0

    Const N1 = 784
    Const N2 = 100
    Const N3 = 10
    Dim i

    w1 = Zeros(N1)
    For i = 0 To UBound(w1)
        w1(i) = Rands(N2, 0.02)
    Next

    b1 = Zeros(N2)

    w2 = Zeros(N2)
    For i = 0 To UBound(w2)
        w2(i) = Rands(N3, 0.02)
    Next

    b2 = Zeros(N3)
End Function



'各要素が0の1次元配列を返す
Function Zeros(size)
    Dim buf()
    Dim i

    ReDim buf(size - 1)
    For i = 0 To UBound(buf)
        buf(i) = 0
    Next

    Zeros = buf
End Function



'各要素がランダムな数値の1次元配列を返す
Function Rands(size, random_std)
    Dim buf()
    Dim i

    ReDim buf(size - 1)
    Randomize '乱数ジェネレータを初期化
    For i = 0 To UBound(buf)
        buf(i) = random_std * Rnd
    Next

    Rands = buf
End Function



'パラメータをファイルに出力する
Function OutputParameters()
    Dim file
    Dim i, j
    Dim buf

    Set file = New C_FileOpen
    file.Path = ".\" & projectName & "_" & numLearn & ".txt"
    file.Open("w")

    For i = 0 To UBound(w1)
        For j = 0 To UBound(w1(i))
            file.Write(w1(i)(j))
        Next
    Next

    For i = 0 To UBound(b1)
        file.Write(b1(i))
    Next

    For i = 0 To UBound(w2)
        For j = 0 To UBound(w2(i))
            file.Write(w2(i)(j))
        Next
    Next

    For i = 0 To UBound(b2)
        file.Write(b2(i))
    Next

    MsgBox file.Path & "に出力しました。"
    file.Close
    Set file = Nothing
End Function



'ファイルからパラメータを読み込む
Function LoadParameters(filePath)
    'ファイル名のチェックとプロジェクト名、学習回数の読み込み
    Dim reg
    Dim matches

    Set reg = CreateObject("VBScript.RegExp") '正規表現オブジェクト
    reg.Pattern = "\\([^\\]+)_(\d+)\.txt$"
    If reg.Test(filePath) Then
        Set matches = reg.Execute(filePath)
        projectName = matches(0).submatches(0) 'プロジェクト名
        numLearn = CLng(matches(0).submatches(1)) '学習回数
    Else
        Msgbox filePath & "はパラメータファイルではないようです。"
        WScript.Quit()
    End If

    'パラメータの読み込み
    Const N1 = 784
    Const N2 = 100
    Const N3 = 10

    Dim file
    Dim i, j
    Dim buf

    Set file = New C_FileOpen
    file.Path = filePath
    file.Open("r")

    w1 = Zeros(N1) 'w1
    For i = 0 To UBound(w1)
        w1(i) = Zeros(N2)
        For j = 0 To UBound(w1(i))
            w1(i)(j) = file.Read
        Next
    Next

    b1 = Zeros(N2) 'b1
    For i = 0 To UBound(b1)
         b1(i) = file.Read
    Next

    w2 = Zeros(N2) 'w2
    For i = 0 To UBound(w2)
        w2(i) = Zeros(N3)
        For j = 0 To UBound(w2(i))
            w2(i)(j) = file.Read
        Next
    Next

    b2 = Zeros(N3) 'b2
    For i = 0 To UBound(b2)
        b2(i) = file.Read
    Next

    file.Close
    Set file = Nothing
End Function



Function Suiron()

End Function



Function OutputAccuracy()

End Function



Function Gakushu()

End Function



'**********************************************************
'クラス宣言
'**********************************************************
Class C_FileOpen
    Private m_fso
    Private m_path
    Private m_file


    Private Sub Class_Initialize
        Set m_fso = CreateObject("Scripting.FileSystemObject")
    End Sub



    Private Sub Class_Terminate
        Set m_fso = Nothing
        If TypeName(m_file) = "TextStream" Then
            m_file.Close
        End If
        Set m_file = Nothing
    End Sub



    Public Property Let Path(val)
        If Left(val, 1) = "." Then
            m_path = m_fso.GetAbsolutePathName(WScript.ScriptFullName & "\..\" & val)
        Else
            m_path = m_fso.GetAbsolutePathName(val)
        End If
    End Property



    Public Property Get Path()
        Path = m_path
    End Property



    Public Property Get HasNext()
        HasNext = Not m_file.AtEndOfStream
    End Property



    Public Function Open(mode)
        Select Case LCase(mode)
        Case "r"
            Set m_file = m_fso.OpenTextFile(m_path, 1, False)
        Case "w"
            Set m_file = m_fso.OpenTextFile(m_path, 2, True)
        Case "a"
            Set m_file = m_fso.OpenTextFile(m_path, 8, True)
        End Select
    End Function



    Public Function Read()
        Read = m_file.ReadLine
    End Function



    Public Function Write(val)
        m_file.WriteLine(val)
    End Function



    Public Function Close()
        m_file.Close()
    End Function

End Class